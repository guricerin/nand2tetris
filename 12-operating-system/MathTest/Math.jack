// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {
    static Array _twoToThe;

    /** Initializes the library. */
    function void init() {
        var int i;
        let _twoToThe = Array.new(16);
        let _twoToThe[0] = 1;
        let i = 1;
        while (i < 16) {
            let _twoToThe[i] = _twoToThe[i-1] + _twoToThe[i-1];
            let i = i + 1;
        }
        return;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        // <=, >= はjack文法に存在しない
        if (x < 0) {
            return -x;
        }
        return x;
    }

    /* xの下位からj番目のビット */
    function int bit(int x, int j) {
        return ~((_twoToThe[j] & x) = 0);
    }

    /** Returns the product of x and y.
     *  When a Jack compiler detects the multiplication operator '*' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, i;
        let sum = 0;
        let shiftedX = x;
        let i = 0;
        while (i < 16) {
            if (Math.bit(y, i)) {
                let sum = sum + shiftedX;
            }
            let shiftedX = shiftedX + shiftedX;
            let i = i + 1;
        }
        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q, res;
        var boolean posi;
        let posi = ((x < 0) = (y < 0));
        let x = Math.abs(x);
        let y = Math.abs(y);

        if (y > x) {
            return 0;
        }
        let q = Math.divide(x, y + y);
        if ((x - (2 * q * y)) < y) {
            let res = q + q;
        } else {
            let res = q + q + 1;
        }

        if (~posi) {
            let res = -res;
        }
        return res;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, i, cond, condPow;
        let y = 0;
        let i = 7;
        // i >= 0
        while (~(i < 0)) {
            let cond = y + _twoToThe[i];
            let condPow = cond * cond;
            // 0 < condPow <= x
            if (~(condPow > x) & (condPow > 0)) {
                let y = cond;
            }
            let i = i - 1;
        }
        return y;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a < b) {
            return b;
        }
        return a;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
            return a;
        }
        return b;
    }
}
